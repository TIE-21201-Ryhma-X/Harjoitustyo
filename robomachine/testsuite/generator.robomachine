*** Settings ***
Suite Setup       Open Browser  ${LOGIN_PAGE_URL}   Chrome      options=add_argument("--headless");add_argument("--no-sandbox")
Suite Teardown    Close Browser
Test Setup        Go to  ${LOGIN_PAGE_URL}
Library           SeleniumLibrary

*** Variables ***
${USERNAME_FIELD}    input-username
${PASSWORD_FIELD}    input-password
${LOGIN_BUTTON}      album-login
${VALID_USERNAME}    user
${VALID_PASSWORD}    password
${LOGIN_PAGE_URL}    http://picture.app:8080/ps/v1/index.html

*** Machine ***

Login Page
  Element Should Be Visible   id = login-window
  [Actions]
    Submit Valid Credentials  ==>    Album Page
    Submit Invalid Credentials  ==>    Login Page

Album Page
  Element Should Be Visible   id = view-list
  [Actions]
    Click Any Image  ==>    Picture Page

Picture Page
  Element Should Be Visible   id = view-full
  [Actions]
    Close Picture Page  ==>    Album Page
    Open Next Image  ==>    Picture Page
    Open Previous Image  ==>      Picture Page

*** Keywords ***
Submit Valid Credentials
  Submit Credentials  ${VALID_USERNAME}  ${VALID_PASSWORD}

Submit Invalid Credentials
  Submit Credentials  invalid  invalid

Submit Credentials
  [Arguments]     ${username}        ${password}
  Input Text    id: input-username    ${USERNAME}
  Input Password    id: input-password    ${PASSWORD}
  Click Button    id: album-login

Click Any Image
  Click Element    css: img

Close Picture Page
  Click Element    id: view-full-close

Open Next Image
  Click Element    id: view-full-next

Open Previous Image
  Click Element    id: view-full-previous